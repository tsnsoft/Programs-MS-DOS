{ Моделирование рабочего процесса включения сцепления при трогании автомобиля}
{ -------------------------------------------------------------------------- }
program Lena_KSU;
uses crt, graph;
{ ************************ Исходные данные задания ************************* }
const w2nach=0;      { Начальная угловая скорость ведомого диска сцепления }
      wN=560;        { Номинальная угловая скорость вращения двигателя }
      w1xx=0.15*wN;  { Приведенная угловая скорость вращения двигателя }
      Ga=1100;       { Вес автомобиля }
      Me=89;         { Момент двигателя }
      J1=0.19;       { Момент инерции сцепления с двигателем }
      NT=0.92;       { Коэффициент трансмиссии }
      ik=3.75;       { Передаточное число коробки передачи }
      i0=4.3;        { Передаточное число главной передачи }
      ee=0.035;      { Коэффициент сопротивления передвижения }
      rk=0.3;        { Радиус колеса }
      k1=150;        { Коэффициент сцепления }
      t0=0;          { Начальное время моделирования }
      tt=2;          { Конечное время моделирования }
      mk=20;         { Число отрезков времени моделирования }
      dt=tt/mk;      { Шаг времени моделирования }
{ *************************** Расчетные величины *************************** }
var   J2,            { Момент инерции сцепления с ведомым диском сцепления }
      w1nach,        { Начальная угловая скорость коленчатого вала }
      Mc,            { Момент сопротивления движению автомобиля }
      w1,            { Угловая скорость коленчатого вала двигателя }
      w2,            { Угловая скорость ведомого диска сцепления }
      a:   real;     { Угол буксирования сцепления }
{ ************************** Переменные программы ************************** }
      data: array [1..3,1..mk] of real; { Массив расчетных величин }
      max: real; { Максимум из значений величин w1, w2, a }
      kY: real; { Коэффициент масштабирования для w1, w2, a по оси Y }
      t, dX: real; ff: boolean; st: string; { Служебные переменные }
      gd, gm, i, x, xx, Y1, Y2, X1, X2, dY, MaxY: integer;
{ ************************************************************************** }
begin
  { Расчет момента инерции сцепления с ведомым диском сцепления }
  J2:=(1.05*Ga*rk)/(sqr(ik)*sqr(i0));
  { Расчет начальной угловой скорости коленчатого вала }
  w1nach:=wN*(1.5*(w1xx/wN)+30*J2/(J1*wN));
  { Расчет момента сопротивления движению автомобиля }
  Mc:=(ee*Ga*rk)/(ik*i0*NT);

  { Расчет массива угловых скоростей и углов буксирования сцепления }
  t:=t0; a:=0; i:=1; ff:=true;
  while t<=tt do begin
    { Расчет угловой скорости коленчатого вала двигателя }
    w1:=(Me*t/J1)-k1*sqr(t)/(2*J1)+w1nach; data[1,i]:=w1;
    { Расчет угловой скорости ведомого диска сцепления }
    w2:=k1*sqr(t)/(2*J2)-(Mc*t/J2)+w2nach; data[2,i]:=w2;
    { Расчет угла буксирования сцепления }
    a:=a+(w1-w2)*dt; data[3,i]:=a;
       { Инициализация значений max }
    if ff then begin max:=w1; ff:= false; end;
    { Поиск наибольшего из значений угловых скоростей и
      углов буксирования сцепления }
    if w1>max then max:=w1; if w2>max then max:=w2; if a>max then max:=a;
    t:=t+dt; i:=i+1;
  end;

  { Очистка экрана }
  clrscr;
  writeln('Моделирование рабочего процесса включения сцепления '+
          'при трогании автомобиля'); writeln;

  { Распечатка значений угловых скоростей и углов буксирования сцепления }
  t:=0; for i:=1 to mk do begin
    writeln('Время: ',t:3:1,' сек.        ','w1=',data[1,i]:6:2,
            '   ','w2=',data[2,i]:6:2,'   ','a=',data[3,i]:6:2);
    t:=t+dt;
  end; writeln; write('Max=', max:6:2); readln;

  { Включение графического режима }
  InitGraph(gd,gm,'E:\TP\BGI');
  if GraphResult <> grOk then begin
     writeln('Ошибка! Не найден файл "egavga.bgi"'); halt;
  end;

  { Расчет масштабных коэффициентов отображения для величин w1, w2, a }
  MaxY:=GetMaxY; Y1:=round(MaxY*0.1); Y2:=round(MaxY*0.9); dY:=Y2-Y1;
  X1:=round(GetMaxX*0.1); X2:=round(GetMaxX*0.9); dX:=(X2-X1)/(mk-1);
  kY:=dY/max;

  { Рисование фона экрана }
  setfillstyle(1,2); bar(0,0,GetMaxX,MaxY); setfillstyle(1,1);
  bar(X1,Y1,X2,Y2); setcolor(15); settextstyle(0,0,2);
  outtextxy(72,7,'Моделирование рабочего процесса');
  outtextxy(170,25,'включения сцепления'); settextstyle(0,0,0);

  { Рисование графиков угловых скоростей и угла буксирования сцепления }
  for i:=1 to mk-1 do begin x:=round(X1+i*dX); xx:=round(X1+(i-1)*dX);
    setcolor(2); line(xx,
      MaxY-round(data[1,i]*kY+Y1),x,MaxY-round(data[1,i+1]*kY+Y1));
    setcolor(12); line(xx,
      MaxY-round(data[3,i]*kY+Y1),x,MaxY-round(data[3,i+1]*kY+Y1));
    setcolor(14); line(xx,
      MaxY-round(data[2,i]*kY+Y1),x,MaxY-round(data[2,i+1]*kY+Y1));
  end;

    setcolor(5); xx:=10;
    { Рисование шкалы времени по оси X }
    outtextxy(X1+170, Y2+3,'Время моделирования'); dX:=(X2-X1)/xx;
    for i:=0 to xx do begin
      x:=round(X1+i*dX); str(i*(tt-t0)/xx:4:1,st);
      outtextxy(x-18,Y2+18,st+'c.');
    end;
    { Рисование шкалы значений по оси Y }
    settextstyle(0,1,0); outtextxy(X1-20, Y1+100,'Моделируемые значения');
    settextstyle(0,0,0); dX:=(Y2-Y1)/xx;
    for i:=1 to xx+1 do begin
      x:=round(Y2-(i-1)*dX); str((i-1)*(max)/xx:4:1,st);
      outtextxy(X2+10,x,st);
    end;

  { Рисование надписей на графиках }
  setcolor(11);
  outtextxy(X1+15,Y1+10,'Угловая скорость колен. вала двигателя');
  setcolor(14);
  outtextxy(X1+50,Y2-15,'Угловая скорость ведомого диска сцепления');
  setcolor(12);
  outtextxy(X1+100,MaxY div 2,'Угол буксирования сцепления');

  { Переключение монитора в текстовый режим }
  readln; CloseGraph;
end.
